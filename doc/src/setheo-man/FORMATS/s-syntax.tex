%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%   SETHEO MANUAL
%	(c) J. Schumann, O. Ibens
%	TU Muenchen 1995
%
%	%W% %G%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\section{\SAM\ Assembler Syntax}
\label{sec:s-syntax}

Assembler code for the SETHEO Abstract machine \SAM\ is typically
generated by the compiler {\bf inwasm}. Per default, \SAM-assembler
code is kept in files with the extension {\bf .s}. The assembler code
can be processed by {\bf wasm} in order to generate binary code for the
\SAM.
The assembler language of SETHEO is defined in a very straight forward
way. Each line of the input file can be empty, may contain
a {\em directive\/}, a {\em label} or an {\em assembler instruction}.

\begin{verbatim}
assembler_code ::=
                line '\n'
                | line '\n' assembler_code

line           ::= 
                LABEL ':' 
                | '.' directive
                | statement 

LABEL          ::= [A-Za-z][A-Za-z0-9_]*
\end{verbatim}

Comments are C-like and enclosed in {\tt /* \ldots */}.

\subsection{Directives}
Each directive starts with a '.', followed by an identifier and
arguments. Directives control the operation of the assembler and do
not generate any code.

\begin{verbatim}
directive      ::=
                'include' STRING
                | 'equ' LABEL numexpr
                | 'dw' exprlist
                | 'ds' numexpr
                | 'start' numexpr
                | 'org' numexpr
                | 'symb' STRING ',' symbtype ',' numexpr
                | 'clause' numexpr ',' numexpr
                | 'red' numexpr
                | 'optim'
                | 'noopt'

symbtype       ::=
                'const' | 'var' | 'pred' | 'global' | 'gterm' | 'ngterm'

exprlist       ::=
                expr
                | exprlist ',' expr

exprlist       ::=
                numexpr
                | TAG numexpr

TAG            ::=
                'const' | 'cref' | 'eostr' | 'var' | 'gterm'
                | 'ngterm' | 'crterm' | 'cstrvar'
\end{verbatim}

A numeric expression {\tt numexpr} can be a number, a label or a
sum or difference of numeric expressions. Labels need not be defined
when they are used. However, if a label occurs in a sum or difference,
it must be defined in order to yield a defined result.
Both {\tt +} and {\tt -} are left associative.

\begin{verbatim}
numexpr        ::=
                LABEL
                | NUMBER
                | numexpr '+' numexpr
                | numexpr '-' numexpr

NUMBER         ::= [0-9][0-9]*
                | -[0-9][0-9]*
\end{verbatim}

\noindent{\bf include}
includes the named file {\tt STRING}. Nested includes are possible up
to 8 levels.

\noindent{\bf equ}
With a directive of the form {\tt .equ name expr}, a macro-name {\tt name}
is assigned the value of {\tt expr}.

\noindent{\bf dw}
This directive allows to set one memory cell with a (tagged or untagged)
word.

\noindent{\bf ds}
A directive {\tt .ds number} reserves {\tt number} consecutive
memory cells. The value of the number must be known during the
first pass.

\noindent{\bf start}
This directive allows the \SAM\ to start at a given address
(default: 0).

\noindent{\bf org}
Places the following code at a given memory address

\noindent{\bf symb}
This directive defines a symbol. Since the relative line number of
this directive (w.r.t.\ the first {\tt .symb}) line is used to determine
the index into the symbol table, no entries must be added, exchanged or
deleted.

\noindent{\bf clause}
This directive marks the beginning of a specific contrapositive or clause.
This directive is used for debugging and readability purposes only.

\noindent{\bf red}
This directive marks code for performing reduction steps for a
specific predicate symbol.
This directive is used for debugging and readability purposes only.

\noindent{\bf optim, noopt}
These directives mark the begin and end of pieces of code to be optimized,
respectively.

\subsection{Statements}

\begin{verbatim}
statement      ::=
                instruction0
                | instruction1 expr
                | instruction2 expr ',' expr
                | instruction3 expr ',' expr ',' expr

instruction0   ::=
                'stop' | 'told' | ...

instruction1   ::=
                'alloc' | 'isunifiable' | ...

instruction2   ::=
                'assign' | 'call' | ...

instruction3   ::=
                'eqpred' | 'porbranch' | ...

expr           ::=
                NUMBER
                | LABEL
\end{verbatim}
